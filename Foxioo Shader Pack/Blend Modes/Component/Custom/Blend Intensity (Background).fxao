/***********************************************************/

/* Autor shader: Foxioo */
/* Version shader: 1.0 (29.08.2025) */
/* My GitHub: https://github.com/FoxiooOfficial */

/***********************************************************/

/* ####################################################### */

//@Begin_vertex

#ifdef GL_ES
 precision mediump float;
#endif
uniform mat4 transformMatrix;
uniform mat4 projectionMatrix;

attribute vec4 position;
attribute vec2 texCoord;
varying vec2 In;

void main(void)
{
	In = texCoord;
	gl_Position = projectionMatrix * transformMatrix * position;
}

//@End

/* ####################################################### */

//@Begin_fragment

#ifdef GL_ES
 precision mediump float;
#endif

varying vec2 In;

/***********************************************************/
/* Samplers */
/***********************************************************/

uniform sampler2D imgTexture;
uniform sampler2D bckgTexture;

uniform vec4 blendColor;
uniform float fPixelWidth;
uniform float fPixelHeight;

/***********************************************************/
/* Variables */
/***********************************************************/

uniform float _Mixing;
uniform bool _Is_Pre_296_Build;

/************************************************************/
/* Main */
/************************************************************/

/*  *NOT* All color calculations are taken from: 
    https://printtechnologies.org/standards/files/pdf-reference-1.6-addendum-blend-modes.pdf */

float Fun_Value(vec3 _Result)
{
    return max(_Result.r, max(_Result.g, _Result.b));
}

vec3 Fun_ClipColor(vec3 _Color)
{
    float _V = Fun_Value(_Color);
    float _ColorMin = min(_Color.r, min(_Color.g, _Color.b));
    float _ColorMax = max(_Color.r, max(_Color.g, _Color.b));

    if(_ColorMin < 0.0) { _Color = _V + (((_Color - _V) * _V) / (_V - _ColorMin)); }
    if(_ColorMax > 1.0) { _Color = _V + (((_Color - _V) * (1.0 - _V)) / (_ColorMax - _V)); }

    return _Color;
}

float Fun_Intensity(vec3 _Result)
{
    return (_Result.r + _Result.g + _Result.b) / 3.0;
}

vec3 Fun_SetIntensity(vec3 _Color, float _I)
{
    float _GetIntensity = _I - Fun_Intensity(_Color);
    _Color += _GetIntensity;

    return Fun_ClipColor(_Color);
}

float Fun_Sat(vec3 _Color)
{
    float _ColorMin = min(_Color.r, min(_Color.g, _Color.b));
    float _ColorMax = max(_Color.r, max(_Color.g, _Color.b));

    return _ColorMax - _ColorMin;
}

vec3 Fun_SetSat(vec3 _Color, float _Sat)
{
    float _CurSat = Fun_Sat(_Color);

    if (_CurSat > 0.0) { _Color = mix(vec3(0.5), _Color, _Sat / _CurSat); }
    else { _Color = vec3(0.5); }
    
    return _Color;
}

/***********************************************************/

void main(void)
{
    vec2 In_Background = vec2(In.x, 1.0 - In.y);

    vec4 _Render_Texture = texture2D(imgTexture, In) * blendColor;
    vec4 _Render_Background = _Is_Pre_296_Build ? texture2D(bckgTexture, In_Background) : texture2D(bckgTexture, In);

    vec4 _Render, _Result;

    _Render.rgb = Fun_SetIntensity(_Render_Background.rgb, Fun_Intensity(_Render_Texture.rgb));

    _Result.rgb = mix(_Render_Texture.rgb, _Render.rgb, _Mixing);
    _Result.a = _Render_Texture.a;

    gl_FragColor = _Result;
}

//@End